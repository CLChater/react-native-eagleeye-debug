buildscript {
    // The Android Gradle plugin is only required when opening the android folder stand-alone.
    // This avoids unnecessary downloads and potential conflicts when the library is included as a
    // module dependency in an application project.
    if (project == rootProject) {
        repositories {
            mavenCentral()
            google()
        }
        def buildGradleVersion = ext.has('buildGradlePluginVersion') ? ext.get('buildGradlePluginVersion') : '4.2.0'

        dependencies {
            classpath "com.android.tools.build:gradle:$buildGradleVersion"
        }
    }
}

plugins {
    id 'com.android.library'
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def verCode = 1
def verName = "1.0.0"

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 30)

    def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
    // Check AGP version for backward compatibility reasons
    if (agpVersion.tokenize('.')[0].toInteger() >= 7) {
        namespace = "com.coolook.eagleye"
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion safeExtGet('targetSdkVersion', 28)

        versionCode verCode
        versionName verName

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'  // 确保包含设备所用的 ABI
        }
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17 -frtti -fexceptions"  // 确保支持 RTTI 和异常
            }
        }
    }
    lintOptions {
        warning 'InvalidPackage', 'MissingPermission'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        buildConfig = true
    }
//      repositories {
//              flatDir {
//                  dirs 'libs'
//              }
//          }
}
repositories {
    mavenCentral()
    google()
    maven { url "https://raw.githubusercontent.com/CLChater/sdk_eagleeye/main" }
//     maven { url "https://gitee.com/simitalk/sdk_eagleeye/raw/main" }
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../node_modules/react-native/android"
    }
//     flatDir {
//          dirs 'libs'
//     }
}

dependencies {
//  compileOnly(name: 'hawkeyeLib-debug', ext: 'aar')
    api "com.sc.eagleeye:sdk.eagleeye:1.0.4"
//     api "com.sc.eagleeye:sdk.eagleeye:1.0.5" // 1.0.5指向本地测试服务器
    implementation "com.facebook.react:react-native:${safeExtGet('reactNativeVersion', '+')}"
}
